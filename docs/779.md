# Verilog 用户定义原语

> 原文：<https://www.javatpoint.com/verilog-user-defined-primitives>

一种建模技术，用户可以通过设计和指定称为用户定义原语(UDPs)的新原语元素来对预定义的门原语进行虚拟论证。这些原语是独立的，不会实例化其他原语或模块。

[Verilog](https://www.javatpoint.com/verilog) 提供了一组标准的原语，如 ***AND，NAND，*** NOT， ***OR，*** 和 ***NOR*** 作为语言的一部分。这些也被称为内置原语。

这些新的 UDP 实例可以用与门原语相同的方式来表示被建模的电路。这种技术可以减少内存量，提高仿真性能。Verilog-XL 算法加速了对这些 UDP 的评估。

然而，设计师偶尔喜欢在开发设计时使用他们定制的原语。

每个 UDP 只有一个输出，可以是以下状态之一:0、1 或 x。不支持三态值 z。任何具有 Z 值的输入都将被视为 x。

这两种行为可以用用户定义的原语来表示:

1.  组合 UDP
2.  顺序 UDP

顺序 UDP 使用其输入值和其输出的当前值来确定下一个输出值。

时序 UDP 为时序电路建模提供了一种高效且简单的方法，例如 ***锁存器和*** 触发器。

顺序 UDP 可以模拟级别敏感和边缘敏感的行为。组合 UDP 的最大输入数是 10。顺序 UDP 的最大输入数被限制为 9，因为内部状态算作输入。

**语法**

UDP 以保留字*开头，以 ***结尾*** 结束。原语的端口/终端应该跟随。UDP 应该在 ***模块*** 和 ***端模块*** 之外定义。*

 *```
primitive UDP_name (output, input, ...);
  port_declaration
  [ reg output; ]
  [ initial output = initial_value; ]
  table
     truth_table
  endtable
endprimitive

```

**UDP 规则**

*   UDP 只接受标量输入端(1 位)。
*   UDP 只能有一个标量输出。输出终端必须总是出现在终端列表的第一位。
*   顺序 UDP 中的状态是用初始语句初始化的。
*   状态表条目可以包含 0、1 或 X 的值。传递给 UDP 的 Z 值被视为 X 值。
*   UDP 与模块在同一级别定义。
*   UDP 完全像门原语一样被实例化。
*   UDP 不支持输入端口。

### 验证日志 UDP 符号

Verilog 用户定义原语可以在与模块定义相同的级别编写，但绝不能在*模块和 ***endmodule*** 之间编写。它们可以有许多输入端口，但总是有一个输出端口，双向端口无效。所有端口信号必须是标量，这意味着它们必须是 1 位宽。*

 *硬件行为被描述为一个原始状态表，该表列出了 ***表*** 和 ***端表*** 中输入及其相应输出的不同可能组合。输入和输出信号的值用下列符号表示。

| 标志 | 评论 |
| Zero | 逻辑 0 |
| one | 逻辑 1 |
| x | 未知，可以是逻辑 0 或 1。它可以用作顺序 UDP 的输入/输出或当前状态 |
| ？ | 逻辑 0、1 或 x。它不能是任何 UDP 的输出 |
| - | 没有变化，只允许在一个 UDP 的输出 |
| 腹肌 | 值从 a 到 b 的变化，其中 a 或 b 是 0、1 或 x |
| * | 和？？，表示输入值的任何变化 |
| r | 与 01 ->输入上升沿相同 |
| f | 与输入端的 10 ->下降沿相同 |
| p | 输入端的潜在正边沿；0->1、0->x 或 x->1 |
| n | 输入端的潜在下降沿；1->0，x->0，1->x |

### 组合 UDP

在组合式 UDP 中，输出状态仅由当前输入状态决定。每当输入改变状态时，都会计算 UDP，并匹配状态表中的一行。输出状态被设置为该行指示的值。组合 UDP 的最大输入数是 10。

考虑下面的例子，它定义了一个具有两个数据输入的多路复用器，一个控制输入。但是只能有一个输出。

```

// Output should always be the first signal in the port list

primitive mux (out, sel, a, b);
	output out;
	input sel, a, b;

	table
		        //   sel 	a 	b 	            out
			0 	1 	? 	: 	1;
			0 	0 	? 	: 	0;
			1 	? 	0 	: 	0;
			1 	? 	1 	: 	1;
			x 	0 	0 	: 	0;
			x 	1 	1 	: 	1;
	endtable

endprimitive

```

a？表示信号可以是 0、1 或 x，这与最终输出无关。

**例**

下面是一个测试平台模块，它实例化了 UDP 并将输入刺激应用于它。

```

module tb;
  reg 	sel, a, b;
  reg [2:0] dly;
  wire 	out;
  integer i;

  // Instantiate the UDP
  // UDPs cannot be instantiated with port name connection
  mux u_mux ( out, sel, a, b);

  initial begin
    a <= 0;
    b <= 0;

    $monitor("[T=%0t] a=%0b b=%0b sel=%0b out=%0b", $time, a, b, sel, out);

 // Drive a, b, and sel after different random delays
    for (i = 0; i < 10; i = i + 1) begin
      	dly = $random;
      #(dly) a <= $random;
      	dly = $random;
      #(dly) b <= $random;
      	dly = $random;
      #(dly) sel <= $random;
    end
  end
endmodule

```

### 顺序 UDP

顺序 UDP 允许在同一描述中混合级别敏感和边缘敏感的构造。输出端口也应该在 UDP 定义中声明为***【reg】***类型，并且可以在 ***初始*** 语句中随意初始化。

顺序 UDP 取其输入的值和其输出的当前值来确定其输出的下一个值。输出的值也是 UDP 的内部状态。

顺序 UDP 在输入和输出字段之间有一个附加字段，由代表当前状态的“:”分隔。

时序 UDP 为时序电路(如锁存器和触发器)建模提供了一种简单有效的方法。顺序 UDP 的最大输入数被限制为 9，因为内部状态算作输入。有两种顺序 UDP。

**1。电平敏感的 UDP**

对级别敏感的顺序行为以与组合行为相同的方式表示，除了输出被声明为 ***reg*** 类型，并且每个表条目中都有一个附加字段。

这个新字段表示 UDP 的当前状态。顺序 UDP 中的输出字段代表下一个状态。

```

primitive d_latch (q, clk, d);
	output 	q;
	input 	clk, d;
	reg  	q;

	table
		         // clk 	d 		q 	q+
			1 	1 	:	? :	1;
			1 	0 	: 	? : 0;
			0 	? 	: 	? : -;
	endtable

endprimitive

```

在上面的代码中，表格最后一行的连字符“-”表示 q+的值没有变化。

```

module tb;
  reg clk, d;
  reg [1:0] dly;
  wire q;
  integer i;

  d_latch u_latch (q, clk, d);

  always #10 clk = ~clk;

  initial begin
    clk = 0;

    $monitor ("[T=%0t] clk=%0b d=%0b q=%0b", $time, clk, d, q);

    #10;                               // To see the effect of X

    for (i = 0; i < 50; i = i+1) begin
      dly = $random;
      #(dly) d <= $random;
    end

    #20 $finish;
  end
endmodule

```

**2。边缘敏感的 UDP**

在对级别敏感的行为中，输入和当前状态的值足以确定输出值。

边缘敏感行为的不同之处在于，输出的变化是由输入的特定转换触发的。

在下面显示的例子中，D 触发器被建模为 Verilog 用户定义的原语。请注意，时钟的上升沿由 01 或 0 指定？

```

primitive d_flop (q, clk, d);
	output  q;
	input 	clk, d;
	reg 	q;

	table
                	           // clk         d 	 	q 		q+
			// obtain output on rising edge of clk
			(01)	0 	: 	? 	: 	0;
			(01) 	1 	: 	? 	: 	1;
			(0?) 	1 	: 	1 	: 	1;
			(0?) 	0 	: 	0 	: 	0;

			// ignore negative edge of clk
			(?0) 	? 	: 	? 	: 	-;

			// ignore data changes on steady clk
			? 		(??): 	? 	: 	-;
	endtable

endprimitive

```

在随机数量的时钟之后，在测试平台中用随机的 d 输入值实例化和驱动 UDP。

```

module tb;
  reg clk, d;
  reg [1:0] dly;
  wire q;
  integer i;

  d_flop u_flop (q, clk, d);

  always #10 clk = ~clk;

  initial begin
    clk = 0;

    $monitor ("[T=%0t] clk=%0b d=%0b q=%0b", $time, clk, d, q);

    #10;  // To see the effect of X

    for (i = 0; i < 20; i = i+1) begin
      dly = $random;
      repeat(dly) @(posedge clk);
      d <= $random;
    end

    #20 $finish;
  end
endmodule

```

1 个时钟延迟后，输出 q 跟随输入 D，这是 D 触发器的理想行为。

* * ***