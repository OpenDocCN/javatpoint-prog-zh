# 验证日志时间刻度

> 原文：<https://www.javatpoint.com/verilog-timescale>

Verilog 模拟依赖于如何定义时间，因为模拟器需要知道#1 在时间方面的含义。“时间刻度编译器指令”指定了跟随它的模块的时间单位和精度。

**语法**

```

`timescale <time_unit>/<time_precision>
// for example
`timescale 1ns/1ps
`timescale 10us/100ns
`timescale 10ns/1ns

```

***time_unit*** 是延迟和模拟时间的度量，而 ***time_precision*** 指定延迟值在模拟中使用前如何舍入。

使用以下时间刻度结构在同一设计中使用不同的时间单位。设计中的延迟规格不可合成，不能转换为硬件逻辑。

*   ***'时标*** 为时间的基本计量单位和精度。
*   ***$ print timegraph***系统任务显示时间单位和精度。
*   ***$time*** 和 ***$realtime*** 系统功能返回当前时间，默认报告格式可以用另一个系统任务 ***$timeformat*** 更改。

| 性格；角色；字母 | 单位 |
| s | 秒 |
| 女士 | 毫秒 |
| 我们 | 微秒 |
| 纳秒 | 纳秒 |
| 著名图象处理软件 | 皮秒 |
| 满量程 | 飞秒 |

这些规范中的整数可以是 1、10 或 100，指定单位的字符串可以采用上表中提到的任何值。

**例 1: 1ns/1ns**

```

// Declare the timescale where time_unit is 1ns
// and time_precision is also 1ns
`timescale 1ns/1ns

module tb;
	// To understand the effect of timescale, let us
	// drive a signal with some values after some delay
  reg val;

  initial begin
  	// Initialize the signal to 0 at time 0 units
    val <= 0;

    // Advance by 1 time unit, display a message and toggle val
    #1 		$display ("T=%0t At time #1", $realtime);
    val <= 1;

    // Advance by 0.49 time unit and toggle val
    #0.49 	$display ("T=%0t At time #0.49", $realtime);
    val <= 0;

    // Advance by 0.50 time unit and toggle val
    #0.50 	$display ("T=%0t At time #0.50", $realtime);
    val <= 1;

    // Advance by 0.51 time unit and toggle val
    #0.51 	$display ("T=%0t At time #0.51", $realtime);
    val <= 0;

	 // Let simulation run for another 5-time units and exit 
    #5 $display ("T=%0t End of simulation", $realtime);
  end
endmodule

```

第一个延迟语句使用#1，使模拟器等待精确的 1 时间单位，用“时间刻度指令”指定为 1ns。第二个延迟语句使用 0.49，这不到半个时间单位。

然而，时间精度被指定为 1ns，并且模拟器不能小于 1 ns，这使得它能够舍入给定的延迟语句并产生 0ns。所以第二次延迟无法提前模拟时间。

第三个延迟语句正好使用一半的时间单位[hl]#0.5[/lh]，模拟器将再次对该值进行舍入以获得#1，它代表一个完整的时间单位。所以这个在 T = 2 纳秒时被打印出来。

第四个延迟语句使用的值超过时间单位的一半，并且也进行了舍入，使得显示语句以 T = 3ns 打印。执行后，它给出以下输出:

```

ncsim> run
T=1 At time #1
T=1 At time #0.49
T=2 At time #0.50
T=3 At time #0.51
T=8 End of simulation
ncsim: *W,RNQUIE: Simulation is complete.

```

模拟按预期运行了 8 纳秒，但请注意，波形在每纳秒之间没有更小的分频。这是因为时间的精度和时间单位是一样的。

**例 2: 10ns/1ns**

与前一个示例相比，本示例中唯一的变化是时间刻度从 1ns/1ns 更改为 10ns/1ns。所以时间单位是 10ns，精度是 1ns。

```

// Declare the timescale where time_unit is 10ns
// and time_precision is 1ns
`timescale 10ns/1ns

// Testbench is the same as in the previous example
module tb;
	// To understand the effect of timescale, let us
	// drive a signal with some values after some delay
  reg val;

  initial begin
  	// Initialize the signal to 0 at time 0 units
    val <= 0;

    // Advance by 1 time unit, display a message and toggle val
    #1 		$display ("T=%0t At time #1", $realtime);
    val <= 1;

    // Advance by 0.49 time unit and toggle val
    #0.49 	$display ("T=%0t At time #0.49", $realtime);
    val <= 0;

    // Advance by 0.50 time unit and toggle val
    #0.50 	$display ("T=%0t At time #0.50", $realtime);
    val <= 1;

    // Advance by 0.51 time unit and toggle val
    #0.51 	$display ("T=%0t At time #0.51", $realtime);
    val <= 0;

	 // Let simulation run for another 5-time units and exit 
    #5 $display ("T=%0t End of simulation", $realtime);
  end
endmodule

```

实际模拟时间是通过用#指定的延迟乘以时间单位获得的，然后根据精度四舍五入。第一个延迟语句将产生 10ns，第二个给出 14.9，取整为 15ns。

第三条语句同样增加了 5ns (0.5 * 10ns)，总时间变成了 20ns。第四个增加了 5 纳秒(0.51 * 10)，将总时间提前到 25 纳秒。

```

ncsim> run
T=10 At time #1
T=15 At time #0.49
T=20 At time #0.50
T=25 At time #0.51
T=75 End of simulation
ncsim: *W,RNQUIE: Simulation is complete.

```

#### 注:波形中的基本单位是几十纳秒，精度为 1ns。

**例 3: 1ns/1ps**

与前一个示例相比，本示例中唯一的变化是时间刻度从 1ns/1ns 更改为 1ns/1ps。所以时间单位是 1ns，精度是 1ps。

```

// Declare the timescale where time_unit is 1ns
// and time_precision is 1ps
`timescale 1ns/1ps

// Testbench is the same as in the previous example
module tb;
	// To understand the effect of timescale, let us
	// drive a signal with some values after some delay
  reg val;

  initial begin
  	// Initialize the signal to 0 at time 0 units
    val <= 0;

    // Advance by 1 time unit, display a message and toggle val
    #1 		$display ("T=%0t At time #1", $realtime);
    val <= 1;

    // Advance by 0.49 time unit and toggle val
    #0.49 	$display ("T=%0t At time #0.49", $realtime);
    val <= 0;

    // Advance by 0.50 time unit and toggle val
    #0.50 	$display ("T=%0t At time #0.50", $realtime);
    val <= 1;

    // Advance by 0.51 time unit and toggle val
    #0.51 	$display ("T=%0t At time #0.51", $realtime);
    val <= 0;

	 // Let simulation run for another 5-time units and exit 
    #5 $display ("T=%0t End of simulation", $realtime);
  end
endmodule

```

请注意，时间单位已缩放，以匹配 1ps 的新精度值。时间以最小的分辨率表示，在这个例子中是皮秒。

```

ncsim> run
T=1000 At time #1
T=1490 At time #0.49
T=1990 At time #0.50
T=2500 At time #0.51
T=7500 End of simulation
ncsim: *W,RNQUIE: Simulation is complete.

```

### 默认时间刻度

虽然 [Verilog](https://www.javatpoint.com/verilog) 模块预计会在模块之前定义一个时间刻度，但是模拟器可能会插入一个默认的时间刻度。

在 Verilog 细化层次结构中的任何范围应用的实际时间刻度可以使用 system task $ printtimescale 打印，它接受范围作为参数。

```

module tb;
	initial begin
		// Print timescale of this module
		$printtimescale(tb);
		// $printtimescale($root);
	end
endmodule

```

#### 注意:时间刻度指令没有放在此模块之前。模拟器最终应用了 1ns/1ns 时间刻度值。

```

xcelium> run
Time scale of (tb) is  1ns /  1ns
xmsim: *W,RNQUIE: Simulation is complete.

```

### 标准时间刻度范围

默认情况下，放置在文件中的时间刻度指令将应用于遵循该指令的所有模块，直到定义另一个时间刻度指令。

```

`timescale 1ns/1ps

module tb;
  des m_des();
  alu m_alu();

  initial begin
    $printtimescale(tb);
    $printtimescale(tb.m_alu);
	$printtimescale(tb.m_des);
  end
endmodule

module alu;

endmodule

`timescale 1ns/10ps

module des;

endmodule

```

在上例中， ***tb*** 和 ***alu*** 的时间刻度为 1ns/1ns，而 ***des*** 的时间刻度为 1ns/10ps，这是因为在模块定义 ***des*** 之前有指令放置。

```

xcelium> run
Time scale of (tb) is  1ns /  1ps
Time scale of (tb.m_alu) is  1ns /  1ps
Time scale of (tb.m_des) is  1ns /  10ps
xmsim: *W,RNQUIE: Simulation is complete.

```

### 验证日志文件

可以使用“include 指令”将其他文件包含在当前文件中，include 指令是一个预处理器指令，它使编译器在编译之前将包含文件的内容放入。

这相当于简单地将另一个文件的全部内容粘贴到这个主文件中。

```

// main.v
`timescale 1ns/1ps

module tb;
  des m_des();
  alu m_alu();

  initial begin
    $printtimescale(tb);
    $printtimescale(tb.m_alu);
	$printtimescale(tb.m_des);
  end
endmodule

`include "file_alu.v"
`include "file_des.v"

// file_alu.v
module alu;
endmodule

// file_des.v
`timescale 1ns/10ps

module des;

endmodule

```

请注意，结果与前面的示例完全相同。 ***alu*** 获得 1ns/1ps 的时间刻度，因为这是最后一个保持有效的指令，直到编译器发现 ***alu*** 定义坚持将其放在不同的文件中。

***des*** 的时标为 1ns/10ps，因为指令在其定义之前就被替换了。

```

xcelium> run
Time scale of (tb) is  1ns /  1ps
Time scale of (tb.m_alu) is  1ns /  1ps
Time scale of (tb.m_des) is  1ns /  10ps
xmsim: *W,RNQUIE: Simulation is complete.

```

**交换文件可以改变时间刻度。**

包含文件的顺序在重新定义时间刻度指令中起着重要的作用，这在下面的示例中很明显。

```

// main.v
`timescale 1ns/1ps

module tb;
  des m_des();
  alu m_alu();

  initial begin
    $printtimescale(tb);
    $printtimescale(tb.m_alu);
	$printtimescale(tb.m_des);
  end
endmodule

// Swapped order of inclusion
`include "file_des.v"
`include "file_alu.v"

// file_alu.v
module alu;
endmodule

// file_des.v
`timescale 1ns/10ps

module des;

endmodule

```

请注意，模块 ***alu*** 现在的时间刻度为 1ns/10ps。

```

xcelium> run
Time scale of (tb) is  1ns /  1ps
Time scale of (tb.m_alu) is  1ns /  10ps
Time scale of (tb.m_des) is  1ns /  10ps
xmsim: *W,RNQUIE: Simulation is complete.

```

这就是为什么在文件顶部有一个时间刻度指令，以便该文件中的所有模块都采用正确的时间刻度，而不管文件是否包含在内。

但是，这种方法可能会使在不更改每个文件的情况下以不同的时间刻度精度进行编译变得困难。

许多编译器和模拟器还提供了覆盖默认时间刻度值的选项，该选项将应用于所有模块。

* * *